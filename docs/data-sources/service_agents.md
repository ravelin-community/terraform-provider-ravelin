---
page_title: "ravelin_service_agents Data Source - terraform-provider-ravelin"
subcategory: ""
description: |-
  Get all service agents contained in a project level IAM policy.
  Use this data source to get all the google managed service accounts (service agents)that have role bindings in your project level IAM policy. These bindings can then beadded to a google_project_iam_policy resource.
---

# ravelin_service_agents (Data Source)

Get all service agents contained in a project level IAM policy.

Use this data source to get all the google managed service accounts (service agents)that have role bindings in your project level IAM policy. These bindings can then beadded to a `google_project_iam_policy` resource.

This data source is particularly useful when you don't want to manage google's
service agents being added in your authoritative policies and take the risk of
removing bindings which can break your cloud environment. This provides a way of
ignoring these changes.

-> **Note** On it's own this data is pretty useless, you need to combine these
service agents with your own policy bindings and apply these changes through a
`google_project_iam_policy` resource. See example below for more details.

## Example Usage

```terraform
data "ravelin_service_agents" "example" {
  project = "my-project"
}


locals {
  service_agent_policy = jsondecode(
    data.ravelin_service_agents.example.service_agent_policy
  )

  my_project_policy = {
    "roles/editor" = [
      "user:john.doe@email.com",
      "serviceAccount:service-a@my-project.iam.gserviceaccount",
    ],
    "roles/bigquery.admin" = [
      "user:alice.bob@email.com"
    ]
  }

  combined_policy = { for role in distinct(
    concat(
      keys(local.service_agent_policy),
      keys(local.my_project_policy),
    )
    ) : role =>
    concat(
      lookup(local.service_agent_policy, role, []),
      lookup(local.my_project_policy, role, []),
    )
  }
}

data "google_iam_policy" "project" {
  dynamic "binding" {
    for_each = local.combined_policy

    content {
      role    = binding.key
      members = binding.value
    }
  }
}

resource "google_project_iam_policy" "project" {
  project     = "my-project"
  policy_data = data.google_iam_policy.project.policy_data
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `project` (String) Name of the GCP project to fetch service agents for

### Read-Only

- `id` (String) The ID of this resource.
- `service_agent_policy` (String) IAM policy JSON encoded as a string of all service agent bindings

## Usage Notes

**Service agents across different projects**

The data resource will only return service agents intended to be used with your
current project. All other service agents from different projects won't be added
to the output if they are part of the project IAM policy.
