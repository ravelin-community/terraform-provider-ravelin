---
page_title: "ravelin_conditional_bindings Data Source - terraform-provider-ravelin"
subcategory: ""
description: |-
  Get all conditional bindings contained in a project level IAM policy.
  Use this data source to get all the conditional bindings in your project level IAM policy, these conditional bindings can often be managed by third party tooling outside of Terraform. These bindings can then be added to a google_project_iam_policy resource.
---

# ravelin_conditional_bindings (Data Source)

Get all conditional bindings contained in a project level IAM policy.

Use this data source to get all the conditional bindings in your project level IAM policy, these conditional bindings can often be managed by third party tooling outside of Terraform. These bindings can then be added to a `google_project_iam_policy` resource.

This data source is particularly useful when you managed authoritative policies
and conditional bindings are managed outside of Terraform. This could be the
case with specific GCP services which have conditional bindings automatically created.

-> **Note** On it's own this data is pretty useless, you need to combine these
conditional bindings with your own policy bindings and apply these changes through a
`google_project_iam_policy` resource. See example below for more details.

## Example Usage

```terraform
data "ravelin_conditional_bindings" "example" {
  project = "my-project"
}

data "google_iam_policy" "project" {

  binding {
    role = "roles/editor"
    members = [
      "user:john.doe@email.com",
      "serviceAccount:service-a@my-project.iam.gserviceaccount",
    ]
  }

  // Automatically add all other conditional bindings from the data source
  dynamic "binding" {
    for_each = toset(data.ravelin_conditional_bindings.example.bindings)

    content {
      role    = binding.value.role
      members = binding.value.members
      condition {
        title       = binding.value.condition.title
        description = binding.value.condition.description
        expression  = binding.value.condition.expression
      }
    }
  }
}

resource "google_project_iam_policy" "project" {
  project     = "my-project"
  policy_data = data.google_iam_policy.project.policy_data
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `project` (String) Name of the GCP project to fetch conditional bindings for. If not specified, the provider-level project will be used.
- `role` (String) Role to filter conditional bindings by

### Read-Only

- `bindings` (Attributes List) Conditional bindings (see [below for nested schema](#nestedatt--bindings))
- `id` (String) The ID of this resource.

<a id="nestedatt--bindings"></a>
### Nested Schema for `bindings`

Read-Only:

- `condition` (Attributes) (see [below for nested schema](#nestedatt--bindings--condition))
- `members` (List of String) Members of the binding
- `role` (String) Role of the binding

<a id="nestedatt--bindings--condition"></a>
### Nested Schema for `bindings.condition`

Read-Only:

- `description` (String) Description of the condition
- `expression` (String) Expression of the condition
- `title` (String) Title of the condition